@DSL Implicit;
@Behaviour FCCMericCailletaudSingleCrystalViscoPlasticity;
@Author Thomas Helfer, Jean-Michel Scherer;
@Date 02 / 11 / 2022;
@Description {
}

@ModellingHypothesis Tridimensional;
@OrthotropicBehaviour;

@Algorithm NewtonRaphson;
@Epsilon 1.e-14;
@Theta 1;

@Brick StandardElasticity{
  young_modulus1 : "((1-damage)*(1-damage)+kres)*E", //208000,
  young_modulus2 : "((1-damage)*(1-damage)+kres)*E", //208000,
  young_modulus3 : "((1-damage)*(1-damage)+kres)*E", //208000,
  poisson_ratio12 : "nu",
  poisson_ratio23 : "nu",
  poisson_ratio13 : "nu",
  shear_modulus12 : "((1-damage)*(1-damage)+kres)*G", //0.384615384615385", //80000,
  shear_modulus23 : "((1-damage)*(1-damage)+kres)*G", //0.384615384615385", //80000,
  shear_modulus13 : "((1-damage)*(1-damage)+kres)*G" //0.384615384615385", //80000
};

@CrystalStructure FCC;
@SlipSystem<0, 1, -1>{1, 1, 1};
//@InteractionMatrix{1, 1, 0.6, 1.8, 1.6, 12.3, 1.6};
@InteractionMatrix{1, 0., 0., 0., 0., 0., 0.};

//@Parameter n = 1.; //10.0;
//@Parameter K = 1.; //25.;
//@Parameter Q = 0.; //11.43;
//@Parameter b = 0.; //2.1;
//@Parameter tau0 = 1.; //66.62;
//@Parameter d = 0.; //494.0;
//@Parameter C = 0.; //14363;

@MaterialProperty real E;
@MaterialProperty real nu;
@MaterialProperty real G;

@MaterialProperty real n;
@MaterialProperty real K;
@MaterialProperty real Q;
@MaterialProperty real b;
@MaterialProperty real tau0;
@MaterialProperty real d;
@MaterialProperty real C;

//@IntegrationVariable strain g[Nss];
@StateVariable strain g[Nss];
g.setEntryName("PlasticSlip");

@AuxiliaryStateVariable strain p[Nss];
p.setEntryName("EquivalentViscoplasticSlip");

@AuxiliaryStateVariable strain a[Nss];
a.setEntryName("BackStrain");

@AuxiliaryStateVariable stress tau[Nss];
tau.setEntryName("ResolvedShearStress");

@LocalVariable strain dPsi_d;

@AuxiliaryStateVariable strain Psi_pl;
Psi_pl.setEntryName("DissipatedEnergy");

@AuxiliaryStateVariable Stensor epl;
epl.setGlossaryName("PlasticStrain");

@Parameter real kres = 1e-12;
kres.setEntryName("ResidualStiffness");

@ExternalStateVariable real damage;
damage.setGlossaryName("Damage");

@Integrator {
  using size_type = unsigned short;
  const auto& ss = FCCMericCailletaudSingleCrystalViscoPlasticitySlipSystems<real>::getSlipSystems();
  const auto& m = ss.him;
  constexpr const auto eeps = 1.e-12;
  const auto seps = eeps * D(0, 0);
  real exp_bp[Nss];
  for (size_type i = 0; i != Nss; ++i) {
      const auto p_ = p[i] + theta * abs(dg[i]);
      exp_bp[i] = exp(-b * p_);
  }
  dPsi_d = 0.;
  epl = 0. * Stensor::Id();
  const auto gd = power<2>(1-(damage + ddamage)) + kres;
  for (size_type i = 0; i != Nss; ++i) {
    //const auto tau = sig | ss.mus[i];
    tau[i] = sig | ss.mus[i];
    auto r = tau0;
    for (size_type j = 0; j != Nss; ++j) {
      r += Q * m(i, j) * (1 - exp_bp[j]);
    }
    r *= gd;
    const auto da =  //
        (dg[i] - d * a[i] * abs(dg[i])) / (1 + theta * d * abs(dg[i]));
    const auto x = gd * C * (a[i] + theta * da);
    const auto f = max(abs(tau[i] - x) - r, gd*stress(0));
//    if (f > 1.1 * K) {
//      return false;
//    }
    const auto sgn = tau[i] - x > 0 ? 1 : -1;
    // elasticity
    feel += dg[i] * ss.mus[i];
    dfeel_ddg(i) = ss.mus[i];
    // viscoplasticity
    const auto v = pow(f / K, n);
    //const auto dv = n * v / (max(f, seps));
    const auto dv = (n / K) * pow(f / K, n - 1);
    fg[i] -= dt * v * sgn;
    dfg_ddeel(i) = -dt * dv * theta * (ss.mus[i] | D);
    const auto sgn_gi = dg(i) > 0 ? 1 : -1;
    const auto dda_ddg =
        (1 - d * a[i] * sgn_gi) / (power<2>(1 + theta * d * abs(dg[i])));
    dfg_ddg(i, i) += gd * dt * dv * C * theta * dda_ddg;
    for (size_type j = 0; j != Nss; ++j) {
      const auto sgn_gj = dg(j) > 0 ? 1 : -1;
      const auto dr = gd * Q * m(i, j) * theta * b * exp_bp[j] * sgn_gj;
      dfg_ddg(i, j) += dt * dv * dr * sgn;
    }
    dPsi_d += tau[i] * dg[i];
    epl += g[i] * ss.mus[i];
  }
}

@UpdateAuxiliaryStateVariables {
  using size_type = unsigned short;
  for (size_type i = 0; i != Nss; ++i) {
    p[i] += abs(dg[i]);
    const auto da =  //
        (dg[i] - d * a[i] * abs(dg[i])) / (1 + theta * d * abs(dg[i]));
    a[i] += da;
  }
  //const auto gd = (1-damage) * (1-damage) + kres;
  //sig = gd * sig;
  Psi_pl += dPsi_d;
}

@TangentOperator{
  //const auto gd = (1-damage) * (1-damage) + kres;
  Stensor4 iJe;
  getPartialJacobianInvert(iJe);
  Dt = D * iJe;
  //Dt = gd * D * iJe;
}

@DissipatedEnergy{
  Psi_d += dPsi_d;
}

@InternalEnergy {
  //const auto gd = (1 - damage) * (1 - damage) + kres;
  //Psi_s = (0.5 / gd) * (sig | eel);
  const auto gd = power<2>(1-(damage+ddamage)) + kres;
  Psi_s = (0.5/gd) * (sig | eel);
}
