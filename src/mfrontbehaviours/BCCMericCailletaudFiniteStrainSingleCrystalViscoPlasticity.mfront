@DSL ImplicitFiniteStrain;
@Behaviour BCCMericCailletaudFiniteStrainSingleCrystalViscoPlasticity;
@Author Thomas Helfer, Jean-Michel Scherer;
@Date 02 / 11 / 2022;
@Description {
}

@ModellingHypothesis Tridimensional;
@OrthotropicBehaviour;

@Algorithm NewtonRaphson;
@Epsilon 1.e-9;
@Theta 1.;
@CompareToNumericalJacobian false; //true;
//@JacobianComparisonCriterion 1.e-6;

@Brick FiniteStrainSingleCrystal{
  young_modulus1 : "((1-damage)*(1-damage)+kres)*E", //208000,
  young_modulus2 : "((1-damage)*(1-damage)+kres)*E", //208000,
  young_modulus3 : "((1-damage)*(1-damage)+kres)*E", //208000,
  poisson_ratio12 : "nu",
  poisson_ratio23 : "nu",
  poisson_ratio13 : "nu",
  shear_modulus12 : "((1-damage)*(1-damage)+kres)*G", //0.384615384615385", //80000,
  shear_modulus23 : "((1-damage)*(1-damage)+kres)*G", //0.384615384615385", //80000,
  shear_modulus13 : "((1-damage)*(1-damage)+kres)*G", //0.384615384615385", //80000
  shifted_elastic_deformation_gradient : true
};

@CrystalStructure BCC;
@SlipSystems{ <1,1,1>{0,1,-1}, <1,1,1>{1,1,-2} };
//mfront-query --interaction-matrix BCCMericCailletaudFiniteStrainSingleCrystalViscoPlasticity.mfront 
//mfront-query --help-behaviour-queries-list BCCMericCailletaudFiniteStrainSingleCrystalViscoPlasticity.mfront

// 28 independent coefficients, 1st and 22nd on diagonal
//@InteractionMatrix{1, 1, 0.6, 1.8, 1.6, 12.3, 1.6};
/* identity matrix */
/*
@InteractionMatrix{
1., 0., 0., 0., 0., 0., 0.,
0., 0., 0., 0., 0., 0.,
0., 0., 0., 0., 0., 0.,
0., 0., 1., 0., 0., 0.,
0., 0., 0.
};
*/

/* Hoc & Forest */
// a0 = 0.4 // 110 x 110 self
// k1 * a0 = 1.*0.4 = 0.4 // 110 x 110 colinear
// k2 * k1 * a0 = 1.15*1.*0.4 = 0.46 // 110 x 110 non-colinear
// kp1 * a0 = 1.05*0.4 = 0.42 // 110 x 112 colinear
// kp2 * kp1 * a0 = 1.05*1.05*0.4 = 0.441 // 110 x 112 non-colinear
// ks0 * a0 = 1.3*0.4 = 0.52 // 112 x 112 self
// ks0 * k1 * a0 = 1.3*1.*0.4 = 0.52 // 112 x 112 colinear
// ks0 * k2 * k1 * a0 = 1.3*1.15*1.*0.4 = 0.598 // 112 x 112 non-colinear
@InteractionMatrix{
0.4, 0.46, 0.46, 0.4, 0.46, 0.46, 0.46, // 110 x 110
0.42, 0.441, 0.441, 0.441, 0.441, 0.441, 0.42, 0.42, 0.441, 0.441, 0.441, 0.441, 0.441, 0.42, // 110 x 112
0.52, 0.598, 0.598, 0.598, 0.598, 0.52, 0.598 // 112 x 112
};
/**/

//@Parameter n = 1.; //10.0;
//@Parameter K = 1.; //25.;
//@Parameter Q = 0.; //11.43;
//@Parameter b = 0.; //2.1;
//@Parameter tau0 = 1.; //66.62;
//@Parameter d = 0.; //494.0;
//@Parameter C = 0.; //14363;

@MaterialProperty real E;
@MaterialProperty real nu;
@MaterialProperty real G;

@MaterialProperty real n;
@MaterialProperty real K;
@MaterialProperty real Q;
@MaterialProperty real b;
@MaterialProperty real tau0;
@MaterialProperty real d;
@MaterialProperty real C;
@MaterialProperty real H;
@MaterialProperty real GB_distance;
@MaterialProperty real KS;

/*
@MaterialProperty real h1;
@MaterialProperty real h2;
@MaterialProperty real h3;
@MaterialProperty real h4;
@MaterialProperty real h5;
@MaterialProperty real h6;
@MaterialProperty real h7;
*/

//@IntegrationVariable strain g[Nss];
//@StateVariable strain g[Nss];
//g.setEntryName("PlasticSlip");

@LocalVariable real dd;

@AuxiliaryStateVariable strain p[Nss];
p.setEntryName("EquivalentViscoplasticSlip");

@AuxiliaryStateVariable strain a[Nss];
a.setEntryName("BackStrain");

@AuxiliaryStateVariable stress tau[Nss];
tau.setEntryName("ResolvedShearStress");

@LocalVariable strain dPsi_d;

@AuxiliaryStateVariable strain Psi_pl;
Psi_pl.setEntryName("DissipatedEnergy");

@AuxiliaryStateVariable tensor<3u,real> Fp;
Fp.setEntryName("PlasticPartOfTheDeformationGradient");

@AuxiliaryStateVariable real nbact;
nbact.setEntryName("NumberOfActiveSlipSystems");

@Parameter real kres = 1e-4;
kres.setEntryName("ResidualStiffness");

@ExternalStateVariable real damage;
damage.setGlossaryName("Damage");

@AuxiliaryStateVariable real gcum;
gcum.setEntryName("AccumulatedPlasticSlip");

@InitLocalVariables{
  dd = max(GB_distance,1.); //e-9);
}

@Integrator {
  using size_type = unsigned short;
  const auto& m = ss.him;
  constexpr const auto eeps = 1.e-12;
  const auto seps = eeps * D(0, 0);
  real exp_bp[Nss];
  for (size_type i = 0; i != Nss; ++i) {
      const auto p_ = p[i] + theta * abs(dg[i]);
      exp_bp[i] = exp(-b * p_);
  }
  dPsi_d = 0.;
  Fp = tensor<3u,real>::Id();
  //const auto damage_ets = damage + ddamage;
  const auto gd = power<2>(1-(damage + ddamage)) + kres;
  for (size_type i = 0; i != Nss; ++i) {
    //const auto tau = M | ss.mu[i];
    tau[i] = M | ss.mu[i];
    auto r = tau0 + (KS/dd);
    auto Q_GB_distance = Q; // + Q * (KS / dd);
    auto H_GB_distance = H; // + H * (KS / dd);
    for (size_type j = 0; j != Nss; ++j) {
      r += Q_GB_distance * m(i, j) * (1 - exp_bp[j]);
    }
    r += H_GB_distance * ( p[i] + theta * abs(dg[i]) );
    r *= gd;
    const auto da =  //
        (dg[i] - d * a[i] * abs(dg[i])) / (1 + theta * d * abs(dg[i]));
    const auto x = gd * C * (a[i] + theta * da);
    const auto f = max(abs(tau[i] - x) - r, gd*stress(0));
//    if (f > 1.1 * K) {
//      return false;
//    }
    const auto sgn = tau[i] - x > 0 ? 1 : -1;
    // viscoplasticity
    const auto v = pow(f / K, n);
    //const auto dv = n * v / (max(f, seps));
    const auto dv = (n / K) * pow(f / K, n - 1);
    fg[i] -= dt * v * sgn;
    dfg_ddeel(i) = -dt * dv * (ss.mu[i] | dM_ddeel);
    const auto sgn_gi = dg(i) > 0 ? 1 : -1;
    const auto dda_ddg =
        (1 - d * a[i] * sgn_gi) / (power<2>(1 + theta * d * abs(dg[i])));
    dfg_ddg(i, i) += gd * dt * dv * ( C * theta * dda_ddg + H_GB_distance * theta * sgn_gi * sgn);
    for (size_type j = 0; j != Nss; ++j) {
      const auto sgn_gj = dg(j) > 0 ? 1 : -1;
      const auto dr = gd * Q_GB_distance * m(i, j) * theta * b * exp_bp[j] * sgn_gj;
      dfg_ddg(i, j) += dt * dv * dr * sgn;
    }
    dPsi_d += tau[i] * dg[i];
  }
}

@UpdateAuxiliaryStateVariables {
  using size_type = unsigned short;
  //const auto& ss = FCCMericCailletaudFiniteStrainSingleCrystalViscoPlasticity<real>::getSlipSystems();
  //Fp = tensor<3u,real>::Id();
  auto dgmax = 0.;
  for (size_type i = 0; i != Nss; ++i) {
    p[i] += abs(dg[i]);
    const auto da =  //
        (dg[i] - d * a[i] * abs(dg[i])) / (1 + theta * d * abs(dg[i]));
    a[i] += da;
    dgmax = max(abs(dg[i]),dgmax);
    gcum += abs(dg[i]);
  }
  nbact = 0.;
  if (dgmax > 0.) {
    for (size_type i = 0; i != Nss; ++i) {
      nbact += abs(dg[i]) / dgmax;
    }
  }
  Psi_pl += dPsi_d;
  Fp = invert(Fe+tensor<3u,real>::Id())*F1;
}

/*
@TangentOperator<DTAU_DDF,Append,AtEnd>{ //<DTAU_DDF>{
  const auto gd = (1-damage) * (1-damage) + kres;
  Dt = gd * Dt;
}
*/

@DissipatedEnergy{
  //Psi_d += dPsi_d;
  const auto gd = power<2>(1-(damage+ddamage)) + kres;
  Psi_d += (dPsi_d / gd);
}

@InternalEnergy {
  //const auto gd = (1 - damage) * (1 - damage) + kres;
  //Psi_s = (0.5 / gd) * (sig | eel);
  const auto gd = power<2>(1-(damage+ddamage)) + kres;
  Psi_s = (0.5 / gd) * (convertCauchyStressToSecondPiolaKirchhoffStress(sig, Fe+tensor<3u,real>::Id()) | eel);
}
